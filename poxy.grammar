
<document> := <command>*

<command> := <assignment> | <rescope> | <link> | <each> | <expr> | <nop>

<assignment> := <list_assignment> | <node_assignment>

<node_assignment> := <node> <left-arrow> <expr>

<list_assignment> := <symbol> <list-op> <symbol> <left-arrow> <expr>

<rescope> := <rescope-op> <node>

<link> := <node> <right-arrow> <node>

<each> := <each-op> <expr> <expr> <expr>

<node> := <symbol_node> | <operator_node> | <root>

<symbol_node> := <path> <symbol>

<path> := <empty> | <explicit_path>

<explicit_path> := <root> | <symbol_node> <dot>

<operator_node> := <path> <operator>

<explicit_operator> := <explicit_path> <operator>

<expr> := <left-paren> <expr> <right-paren> | <call> | <value> | <lambda> | <conditional> | <all> | <list> | <do>

<call> := <infix> | <circumfix> | <prefix>

<infix> := <expr> <operator> <expr>

<circumfix> := <half-operator> <param>* <half-terminator>

<prefix> := <function> <param>*

<function> := <symbol_node> | <explicit_operator>

<param> := <expr> | <free>

<list> := <expr> | <pair>

<pair> := <expr> <list-op> <expr>

<lambda> := <left-bracket> <symbol>* <operator:|> <document> <right-bracket>

<all> := <all-op> <expr> <expr>

<do> := <do-op> <lambda> <param>* 

<conditional> := <ifthen> | <ifthenelse>

<ifthen> := <if-op> <expr> <then-op> <expr> 
<ifthen> := <ifthen> <elseif-op> <expr> <then-op> <expr>

<ifthenelse> := <ifthen> <else-op> <expr>

<value> := <string> | <regexp> | <numeric>

<nop> := <newline> | <terminator> | <comment>

<terminator> := <EOF> | <EOL> | <NUL>

<operator> := <normal-operator> | <auto-circumfix-operator> | <circumfix-operator>

<string> := <single-quoted> | <double-quoted> | <unicode-expr>

<regexp> := <match-regexp> | <subst-regexp>

<numeric> := <binary> | <octal> | <decimal> | <hexidecimal> | <float>

==EOF==

--
-- see P/Lexer.pm for the details of these grammar rules
--

<all-op>
<auto-circumfix-operator>
<binary>
<circumfix-operator>
<comment>
<decimal>
<do-op>
<dot>
<double-quoted>
<each-op>
<elif-op>
<else-op>
<float>
<free>
<half-operator>
<half-terminator>
<hexidecimal>
<if-op>
<is-op>
<left-arrow>
<left-bracket>
<list-op>
<match-regexp>
<newline>
<normal-operator>
<nothing>
<octal>
<paren>
<rescope-op>
<right-arrow>
<right-bracket>
<root>
<single-quoted>
<subst-regexp>
<symbol>
<then-op>
<unicode-expr>
<empty>
